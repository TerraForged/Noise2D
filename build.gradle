apply plugin: "java"
apply plugin: "maven-publish"

group "com.terraforged"
version "0.6.0-SNAPSHOT"

configurations {
    shade
    compile.extendsFrom(shade)
}
repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    shade "com.github.TerraForged:Cereal:master-SNAPSHOT"
}

jar {
    from { configurations.shade.collect { it.isDirectory() ? it : zipTree(it) } }
    duplicatesStrategy = "EXCLUDE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }

    if (System.getenv("MAVEN_USER") != null && System.getenv("MAVEN_PASS") != null) {
        repositories {
            maven {
                credentials {
                    username System.getenv("MAVEN_USER")
                    password System.getenv("MAVEN_PASS")
                }
                name = "nexus"
                url = "https://io.terraforged.com/repository/maven/"
            }
        }
    }
}
